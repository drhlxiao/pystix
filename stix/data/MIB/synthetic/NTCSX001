#
#  This synthetic determines whether the Non-OP or OP regulation
#  limits are used for Thermal Loop #63 METIS ME
#
#  The OPS limits are used when the UVDA sensor is to be switched on
#  and the change happens with procedures TC-FCP-900 and TC-FCP-903
#
#  Since there is no S/C parameter to indicate that the change has been
#  commanded, the raw values of the regulation limits are used. In case
#  these are updated in the procedures, the new values need to be placed
#  in the code below
#
#  The return values of the synthetic are:
#     0 if the non-OP regulation limits are used
#     1 if the OP regulation limits are used
#

# Define return values
VAR_NON_OP := 0;
VAR_OP := 1;

# Define temperature criteria for being in
# These need to be updated if the respective OP range in TC-FCP-900/903 is updated 
VAR_OP_UPPER_RAW := 23977;
VAR_OP_LOWER_RAW := 23793;

# Get current regulation temperatures
VAR_TT063_UPPER_RAW := NTCD53P1.raw;
VAR_TT063_LOWER_RAW := NTCD53P2.raw;

# Determine if we are regulating in the OP range or not
if ((VAR_TT063_UPPER_RAW == VAR_OP_UPPER_RAW) land (VAR_TT063_LOWER_RAW == VAR_OP_LOWER_RAW)) then
   return(VAR_OP);
else
   return(VAR_NON_OP);
endif;